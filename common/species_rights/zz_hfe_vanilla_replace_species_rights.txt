#
citizenship_slavery = {
	token = citizenship_slavery
	in_breach_of = {
		{
			key = resolution_greatergood_balance_in_the_middle
		}
		{
			key = resolution_greatergood_universal_prosperity_mandate
		}
	}
	potential = {
		NAND = {
			exists = this
			OR = {
				has_trait = trait_machine_unit
				has_trait = trait_mechanical
			}
		}
	}
	allow = {
		hidden_trigger = {
			exists = from
		}
		custom_tooltip = {
			fail_text = "HIVE_MIND_MAIN_SPECIES_ALWAYS_CITIZEN"
			NAND = {
				from = {
					has_authority = auth_hive_mind
				}
				has_trait = trait_hive_mind
			}
		}
		custom_tooltip = {
			fail_text = same_species_disallowed
			NOT = {
				is_same_species = from
			}
		}
		custom_tooltip = {
			fail_text = "lonely_hive_mind_pops_kill_themselves_citizenship"
			is_lonely_hive_mind_pop = no
		}
		from = {
			NOR = {
				has_valid_civic = civic_machine_servitor
				has_valid_civic = civic_machine_assimilator
				has_valid_civic = civic_machine_terminator
				has_valid_civic = civic_hive_devouring_swarm
			}
		}
		custom_tooltip = {
			fail_text = empire_xenophile
			from = {
				NOR = {
					has_ethic = ethic_xenophile
					has_ethic = ethic_fanatic_xenophile
				}
			}
		}
		custom_tooltip = {
			fail_text = "FANATIC_PURIFIERS_MUST_PURIFY"
			OR = {
				NOT = {
					from = {
						has_valid_civic = civic_fanatic_purifiers
					}
				}
				is_same_species = from
			}
		}
		custom_tooltip = {
			fail_text = does_not_embrace_species_slavery
			# mod
			or = {
				has_species_flag = hfe_forever_slave
				has_species_flag = hfe_forever_slave@from
				has_trait = hfe_species_trait_happy_slave
				from = {
					allows_slavery = yes
					# OR = {
					# 	has_ethic = ethic_xenophobe
					# 	has_ethic = ethic_fanatic_xenophobe
					# 	has_ethic = ethic_authoritarian
					# 	has_ethic = ethic_fanatic_authoritarian
					# 	has_ethic = ethic_gestalt_consciousness
					# }
				}
			}
		}
		custom_tooltip = {
			fail_text = yes_service_yes_citizenship
			OR = {
				NOT = {
					from = {
						has_valid_civic = civic_citizen_service
					}
				}
				NOT = {
					has_military_service_type = {
						type = military_service_full
						country = from
					}
				}
			}
		}
	}
	ai_will_do = {
		factor = 15
		modifier = {
			factor = 0
			from = {
				has_valid_ai_personality = yes
				NOT = {
					has_ai_personality_behaviour = slaver
				}
				NAND = {
					is_ai = no
					is_xenophobe = yes
				}
			}
		}
		modifier = {
			factor = 0
			from = {
				is_authoritarian = no
				is_xenophobe = no
			}
		}
		modifier = {
			factor = 0.1
			from = {
				is_galactic_community_member = yes
			}
			OR = {
				is_active_resolution = "resolution_greatergood_balance_in_the_middle"
				is_active_resolution = "resolution_greatergood_universal_prosperity_mandate"
			}
		}
	}
}
